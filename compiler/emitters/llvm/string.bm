'Copyright Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'string.bm - Code generation for string handling

function ll_cg_str_constant%&(constant)
    dim as _offset value, global, indices(1)
    s$ = ll_cg_str_header$(ast_constants(constant)) + ast_constants(constant)
    value = llvm_const_string(s$, len(s$), TRUE)
    global = llvm_add_global(ll_cg_state.module, llvm_type_of(value), "")
    llvm_set_initializer global, value
    llvm_set_global_constant global, TRUE
    llvm_set_linkage global, LLVMPrivateLinkage
    llvm_set_unnamed_address global, LLVMGlobalUnnamedAddr
    indices(0) = llvm_const_int(llvm_int32_type, 0, false)
    indices(1) = llvm_const_int(llvm_int32_type, 0, false)
    ll_cg_str_constant%& = llvm_const_in_bounds_gep(global, indices(), 2)
end function

'Create the string type in llvm so we can calculate offsets of header fields.
'This must match up with the foundation library's idea of the LB_STRING type.
function ll_cg_str_type%&
    static string_t as _offset
    if string_t = 0 then
        dim as _offset elements(1 to 5)
        elements(1) = llvm_int8_type%& 'flags
        elements(2) = llvm_int8_type%& 'refcount
        elements(3) = llvm_int32_type%& 'used
        elements(4) = llvm_int32_type%& 'alloc
        elements(5) = llvm_array_type%&(llvm_int8_type%&, 0) 'data
        string_t = llvm_struct_type%&(elements(), ubound(elements), FALSE)
    end if
    ll_cg_str_type%& = string_t
end function

function ll_cg_str_header$(s$)
    dim as _offset string_t
    string_t = ll_cg_str_type
    flags_offset = llvm_offset_of_element(llvm_get_module_data_layout(ll_cg_state.module), string_t, 0)
    'no need for refcount
    used_offset = llvm_offset_of_element(llvm_get_module_data_layout(ll_cg_state.module), string_t, 2)
    alloc_offset = llvm_offset_of_element(llvm_get_module_data_layout(ll_cg_state.module), string_t, 3)
    'header size including padding is given by offset of start of data
    header_size = llvm_offset_of_element(llvm_get_module_data_layout(ll_cg_state.module), string_t, 4)
    header$ = string$(header_size, chr$(0))
    mid$(header$, flags_offset + 1, 1) = chr$(1) 'LB_STRING_READONLY
    mid$(header$, used_offset + 1, 4) = mkl$(len(s$))
    mid$(header$, alloc_offset + 1, 4) = mkl$(len(s$))
    ll_cg_str_header$ = header$
end function

sub ll_cg_str_free_transients
    dim as _offset dummy
    dim as ll_arg_t args(1 to 1)
    for i = 1 to ll_cg_str_last_queued_transient
        args(1) = ll_cg_str_queued_transients(i)
        dummy = ll_cg_call(TOK_STRING_MAYBE_FREE, -1, args())
    next i
    ll_cg_str_last_queued_transient = 0
end sub

sub ll_cg_str_queue_transient(s as ll_arg_t)
    u = ubound(ll_cg_str_queued_transients)
    if ll_cg_str_last_queued_transient = u then
        redim _preserve ll_cg_str_queued_transients(u * 2) as ll_arg_t
    end if
    ll_cg_str_last_queued_transient = ll_cg_str_last_queued_transient + 1
    ll_cg_str_queued_transients(ll_cg_str_last_queued_transient) = s
end sub

function ll_cg_str_assign%&(rvalue as _offset, lvalue as _offset)
    dim as ll_arg_t args(1 to 2)
    args(1).lp = lvalue
    args(1).is_byval = FALSE
    args(2).lp = rvalue
    args(2).is_byval = TRUE
    ll_cg_str_assign%& = ll_cg_call(TOK_STRING_ASSIGN, -1, args())
end function
