'Copyright Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'print.bm - Parse rules for PRINT statement

'Expects: TOK_PRINT
'Results: token after last expression, comma or semicolon
'Format: PRINT [expressionlist][{,|;}]
function ps_print
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start print"
    $end if
    root = ast_add_node(AST_CALL)
    root->ref = TOK_PRINT
    root->ref2 = TOK_PRINT->sig
    ps_consume TOK_PRINT

    newline = TRUE
    do
        if ps_is_terminator(tok_token) then exit do
        select case tok_token
        case TOK_COMMA
            node = ast_add_node(AST_FLAGS)
            node->ref = AST_FLAG_MANUAL
            node->ref2 = PRINT_NEXT_FIELD
            root->attach(node)
            ps_consume TOK_COMMA
            newline = FALSE
        case TOK_SEMICOLON
            'No flags needed here
            ps_consume TOK_SEMICOLON
            newline = FALSE
        case else
            root->attach(ps_expr)
            newline = TRUE
        end select
    loop
    if newline then
        node = ast_add_node(AST_FLAGS)
        node->ref = AST_FLAG_MANUAL
        node->ref2 = PRINT_NEWLINE
        root->attach(node)
    end if
    
    ps_print = root
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed print"
    $end if
end function
