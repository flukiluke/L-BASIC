'Copyright Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'putimage.bm - Parse rules for _PUTIMAGE statement

'Expects: TOK__PUTIMAGE
'Results: NEWLINE
'Format: [[STEP] (single, single) [- [STEP] (single, single)]], [long], [long], [[STEP] (single, single) [- STEP (single, single)]] [, _SMOOTH]

function ps__putimage
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start _putimage"
    $end if

    root = ast_add_node(AST_CALL)
    root->ref = TOK__PUTIMAGE
    root->ref2 = TOK__PUTIMAGE->sig
    ps_consume TOK__PUTIMAGE
    flag_node = ast_add_node(AST_FLAGS)
    flag_node->ref = AST_FLAG_MANUAL
    root->attach(flag_node)

    if ps_consumed(TOK_STEP) then flags = flags OR PUTIMAGE_STEP_SRC1
    if ps_consumed(TOK_OPAREN) then
        root->attach(ps_expr)
        ps_consume TOK_COMMA
        root->attach(ps_expr)
        ps_consume TOK_CPAREN
    else
        root->attach_none
        root->attach_none
    end if
    if ps_consumed(TOK_DASH) then
        if ps_consumed(TOK_STEP) then flags = flags OR PUTIMAGE_STEP_SRC2
        ps_consume TOK_OPAREN
        root->attach(ps_expr)
        ps_consume TOK_COMMA
        root->attach(ps_expr)
        ps_consume TOK_CPAREN
    else
        root->attach_none
        root->attach_none
    end if
    if not ps_consumed(TOK_COMMA) then goto putimage_parse_done
    if tok_token <> TOK_COMMA then root->attach(ps_expr) else root->attach_none
    if not ps_consumed(TOK_COMMA) then goto putimage_parse_done
    if tok_token <> TOK_COMMA then root->attach(ps_expr) else root->attach_none
    if not ps_consumed(TOK_COMMA) then goto putimage_parse_done
    if ps_consumed(TOK_STEP) then flags = flags OR PUTIMAGE_STEP_DEST1
    if ps_consumed(TOK_OPAREN) then
        root->attach(ps_expr)
        ps_consume TOK_COMMA
        root->attach(ps_expr)
        ps_consume TOK_CPAREN
    else
        root->attach_none
        root->attach_none
    end if
    if ps_consumed(TOK_DASH) then
        if ps_consumed(TOK_STEP) then flags = flags OR PUTIMAGE_STEP_DEST2
        ps_consume TOK_OPAREN
        root->attach(ps_expr)
        ps_consume TOK_COMMA
        root->attach(ps_expr)
        ps_consume TOK_CPAREN
    else
        root->attach_none
        root->attach_none
    end if
    if ps_consumed(TOK_COMMA) then
        ps_consume TOK__SMOOTH
        flags = flags OR PUTIMAGE_SMOOTH
    end if

    putimage_parse_done:
    flag_node->ref2 = flags
    'Fill in any missing arguments on the end
    for i = ast_num_children(root) + 1 to 11
        root->attach_none
    next i
    ps__putimage = root

    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed _putimage"
    $end if
end function
