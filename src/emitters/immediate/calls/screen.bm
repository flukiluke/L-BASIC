'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'screen.bm - Executors for screen management commands

case TOK_CLS
    c1 = ast_get_child(node, 1)
    c2 = ast_get_child(node, 2)
    if ast_is_none(c1) and ast_is_none(c2) then
        cls
    elseif not ast_is_none(c1) then
        imm_eval c1, v1
        cls v1.n
    elseif not ast_is_none(c2) then
        imm_eval c2, v2
        cls , v2.n
    else
        imm_eval c1, v1
        imm_eval c2, v2
        cls v1.n, v2.n
    end if

case TOK_COLOR
    v1.n = _defaultcolor
    v2.n = _backgroundcolor
    c1 = ast_get_child(node, 1)
    c2 = ast_get_child(node, 2)
    if not ast_is_none(c1) then imm_eval c1, v1
    if not ast_is_none(c2) then imm_eval c2, v2
    color v1.n, v2.n

case TOK_CSRLIN
    result.n = csrlin

case TOK__DEST
    if return_type = TYPE_NONE then
        imm_eval ast_get_child(node, 1), v1
        _dest v1.n
    else
        result.n = _dest
    end if

case TOK__DISPLAY
    _display

case TOK__HEIGHT
    c1 = ast_get_child(node, 1)
    if ast_is_none(c1) then
        v1.n = _dest
    else
        imm_eval c1, v1
    end if
    result.n = _height(v1.n)

case TOK_LOCATE
    v1.n = csrlin
    v2.n = pos(0)
    imm_eval ast_get_child(node, 1), v1
    imm_eval ast_get_child(node, 2), v2
    cursor = ast_get_child(node, 3)
    imm_eval cursor, v3
    if ast_num_children(node) = 5 then
        imm_eval ast_get_child(node, 4), v4
        imm_eval ast_get_child(node, 5), v5
        if ast_is_none(cursor) then
            locate v1.n, v2.n, , v4.n, v5.n
        else
            locate v1.n, v2.n, v3.n, v4.n, v5.n
        end if
    else
        if ast_is_none(cursor) then
            locate v1.n, v2.n
        else
            locate v1.n, v2.n, v3.n
        end if
    end if

case TOK__SOURCE
    if return_type = TYPE_NONE then
        imm_eval ast_get_child(node, 1), v1
        _source v1.n
    else
        result.n = _source
    end if

case TOK__WIDTH
    c1 = ast_get_child(node, 1)
    if ast_is_none(c1) then
        v1.n = _dest
    else
        imm_eval c1, v1
    end if
    result.n = _width(v1.n)
