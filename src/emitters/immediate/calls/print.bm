'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'print.bm - Executor for PRINT statement

case TOK_PRINT
    for i = 1 to ast_num_children(node)
        child = ast_get_child(node, i)
        if ast_nodes(child).typ = AST_FLAGS then
            if ast_nodes(child).ref2 AND PRINT_NEXT_FIELD then
                print ,
            elseif ast_nodes(child).ref2 AND PRINT_NEWLINE then
                print
            end if
        else
            imm_eval child, v1
            'Need to check type explicitly so we can explicitly give the correct C*() function
            select case type_of_expr(child)
            case TYPE_INTEGER
                print cint(v1.n);
            case TYPE_LONG
                print clng(v1.n);
            case TYPE_INTEGER64
                print _round(v1.n);
            case TYPE_SINGLE
                print csng(v1.n);
            case TYPE_DOUBLE
                print cdbl(v1.n);
            case TYPE_QUAD
                print v1.n;
            case TYPE_STRING
                print v1.s;
            end select
        end if
    next i
