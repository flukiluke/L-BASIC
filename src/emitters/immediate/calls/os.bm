'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'os.bm - Various OS and environment functions

case TOK_COMMAND
    if ast_is_none(ast_get_child(node, 1)) then
        result.s = ""
        for i = input_file_command_offset + 1 to _commandcount
            result.s = result.s + command$(i)
            if i <> _commandcount then result.s = result.s + " "
        next
    else
        imm_eval ast_get_child(node, 1), v1
        result.s = command$(input_file_command_offset + v1.n)
    end if

case TOK__COMMANDCOUNT
    result.n = _commandcount - input_file_command_offset

case TOK_END
    v1.n = 0
    if not ast_is_none(ast_get_child(node, 1)) then imm_eval ast_get_child(node, 1), v1
    'END is only useful when the display will disappear after execution is finished,
    'which is not the case when running in terminal mode.
    $if DEBUG_HEAP then
    if options.debug then imm_heap_stats
    $end if
    if options.terminal_mode then system v1.n else end v1.n
        
case TOK_ENVIRON
    c1 = ast_get_child(node, 1)
    imm_eval c1, v1
    if type_of_expr(c1) = TYPE_STRING then
        result.s = environ$(v1.s)
    else
        result.s = environ$(v1.n)
    end if

case TOK_RMDIR
    c1 = ast_get_child(node, 1)
    imm_eval c1, v1
    rmdir v1.s

case TOK_SYSTEM
    v1.n = 0
    if not ast_is_none(ast_get_child(node, 1)) then imm_eval ast_get_child(node, 1), v1
    $if DEBUG_HEAP then
    if options.debug then imm_heap_stats
    $end if
    system v1.n
