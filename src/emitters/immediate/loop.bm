'Copyright Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'loop.bm - Executor for DO LOOP and WHILE WEND

'Also used for WHILE WEND loops
sub imm_do_do_pre(node)
    dim temp as imm_value_t
    guard = ast_get_child(node, 1)
    block = ast_get_child(node, 2)
    if imm_next_jump = 0 then imm_eval guard, temp
    do while temp.n <> 0 or imm_next_jump <> 0
        imm_run block
        if imm_next_jump then exit sub
        if imm_exit_node > 0 and imm_exit_node = node then
            imm_exit_node = 0
            exit sub
        elseif imm_exit_node > 0 then
            exit sub
        end if
        imm_eval guard, temp
    loop
end sub

sub imm_do_do_post(node)
    dim temp as imm_value_t
    guard = ast_get_child(node, 1)
    block = ast_get_child(node, 2)
    do
        imm_run block
        if imm_next_jump then exit sub
        if imm_exit_node > 0 and imm_exit_node = node then
            imm_exit_node = 0
            exit sub
        elseif imm_exit_node > 0 then
            exit sub
        end if
        imm_eval guard, temp
    loop while temp.n
end sub
