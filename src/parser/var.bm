'Copyright Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'var.bm - Parse rules for DIM and variables

'Expects: TOK_DIM, TOK_REDIM or TOK_STATIC
'Results: token after last declaration
'Format: DIM [STATIC] [_PRESERVE] [SHARED] variablelist
function ps_dim
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start dim"
    $end if
    if tok_token = TOK_DIM or tok_token = TOK_REDIM then tok_advance
    if ps_consumed(TOK_STATIC) then is_static = TRUE
    if ps_consumed(TOK__PRESERVE) then preserve = TRUE
    if ps_consumed(TOK_SHARED) then is_shared = TRUE
    if is_static then
        if is_shared or preserve then ps_error "Cannot be SHARED or _PRESERVE when STATIC"
        if ps_last_nested(AST_PROCEDURE) = 0 then ps_error "Not in function"
        'Switch back to the main program's frame so that statics are allocated
        'in there instead of locally.
        inner_last_var = ps_scoped_last_var
        ps_scoped_last_var = ps_main_last_var
    end if

    'Check for Dim As Long style syntax
    typ = ps_opt_sigil
    do
        name_token = tok_token
        'This is a little messy because we have to look-ahead some to see if
        'it's a scalar or array.
        variable_name$ = tok_content$
        tok_advance
        sigil = ps_opt_sigil
        if typ > 0 and sigil > 0 and typ <> sigil then ps_error "Variable type does not match DIM type"
        if typ then sigil = typ
        if tok_token = TOK_OPAREN or type_is_array(sigil) then
            'Array declaration.
            'The block holds all resizes declared in this DIM statement
            if block = 0 then block = ast_add_node(AST_BLOCK)
            ps_dim_array name_token, variable_name$, sigil, block, is_shared, preserve, is_static
        else
            'Just a regular variable
            if name_token <> TOK_UNKNOWN then ps_error "Expected new variable name"
            sym = ps_new_var_pp(variable_name$, sigil, is_shared, FALSE)
            if is_static then sym->vflags = sym->vflags OR SYM_VARIABLE_MAINFRAME
        end if
    loop while ps_consumed(TOK_COMMA)
    ps_dim = block

    if is_static then
        ps_main_last_var = ps_scoped_last_var
        ps_scoped_last_var = inner_last_var
    end if
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed dim"
    $end if
end function

function ps_lvalue
    token = tok_token
    content$ = tok_content$
    tok_advance
    ps_lvalue = ps_lvalue_p(token, content$)
end function

function ps_lvalue_mutable
    node = ps_lvalue
    sym = node->ref
    if sym->vflags AND SYM_VARIABLE_CONST then
        ps_error "Cannot reassign CONST"
    end if
    ps_lvalue_mutable = node
end function

function ps_lvalue_p(head, content$)
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start lvalue"
    $end if
    node = ps_simple_variable_p(head, content$)
    do while tok_token = TOK_DOT or tok_token = TOK_OPAREN
        if ps_consumed(TOK_DOT) then
            'UDT element access
            node = ps_udt_element_access(node)
        elseif ps_consumed(TOK_OPAREN) and not ps_consumed(TOK_CPAREN) then
            'array access. Something like `a()` is a reference to the entire array,
            'so no access operation needed.
            node = ps_array_element_access(node)
        end if
    loop
    ps_lvalue_p = node
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed lvalue"
    $end if
end function

function ps_simple_variable
    token = tok_token
    content$ = tok_content$
    tok_advance
    ps_simple_variable = ps_simple_variable_p(token, content$)
end function

'Expects: token after variable, variable token as arg
'Results: token after optional sigil
'Note: process simple variables, i.e. no udt or array stuff. Returns AST_VAR.
function ps_simple_variable_p(head, content$)
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start simple variable"
    $end if
    node = ast_add_node(AST_VAR)
    if head = TOK_UNKNOWN then
        'Add new variable (implicit declaration)
        if not ps_allow_implicit_vars then ps_error "Implicit variable declaration"
        node->ref = ps_new_var_p(content$)
    elseif head->stype <> SYM_VARIABLE then
        ps_error "Expected variable"
    else
        'Existing variable
        sigil = ps_opt_sigil
        current_type = head->type
        if current_type->tflags = SYM_TYPE_ARRAY then current_type = current_type->array_type
        if sigil and sigil <> current_type then ps_error "Type suffix does not match existing variable type"
        node->ref = head
    end if

    ps_simple_variable_p = node
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed simple variable"
    $end if
end function

'Expects: sigil or otherwise
'Results: post token if sigil present, unchanged otherwise
function ps_opt_sigil
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start optional sigil"
    $end if
    if ps_consumed(TOK_AS) then
        typ = tok_token
        if typ = 0 or typ->stype <> SYM_TYPE then ps_error "Expected type name"
    else
        select case tok_token
        case TOK_INTEGER_SFX
            typ = TYPE_INTEGER
        case TOK_LONG_SFX
            typ = TYPE_LONG
        case TOK_INTEGER64_SFX
            typ = TYPE_INTEGER64
        case TOK_SINGLE_SFX
            typ = TYPE_SINGLE
        case TOK_DOUBLE_SFX
            typ = TYPE_DOUBLE
        case TOK_QUAD_SFX
            typ = TYPE_QUAD
        case TOK_STRING_SFX
            typ = TYPE_STRING
        case else
            typ = 0
        end select
    end if
    if typ then tok_advance
    if ps_consumed(TOK_OBRACKET) then
        'Array type. If the element type hasn't been given, assume it to
        'be the default type.
        if typ = 0 then typ = ps_default_type
        ps_assert TOK_NUMINT
        dimensions = val(tok_content$)
        tok_advance
        ps_consume TOK_CBRACKET
        typ = type_make_array(typ, dimensions)
    end if
    ps_opt_sigil = typ
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed optional sigil"
    $end if
end function

function ps_new_var_p(var_name$)
    sigil = ps_opt_sigil
    ps_new_var_p = ps_new_var_pp(var_name$, sigil, FALSE, FALSE)
end function

function ps_new_var_pp(var_name$, sigil, is_shared, is_ref)
    dim sym as symtab_entry_t
    if is_shared then
        sym-identifier = ucase$(var_name$)
    else
        sym-identifier = ps_scope$ + ucase$(var_name$)
    end if
    sym-stype = SYM_VARIABLE
    if sigil then sym-type = sigil else sym-type = ps_default_type
    sym-prev_var = ps_scoped_last_var
    if is_shared then sym-vflags = SYM_VARIABLE_MAINFRAME else sym-vflags = 0
    if is_ref then sym-vflags = sym-vflags OR SYM_VARIABLE_DEREF
    symtab_add_entry sym
    ps_scoped_last_var = symtab_last_entry
    ps_new_var_pp = symtab_last_entry
end function
