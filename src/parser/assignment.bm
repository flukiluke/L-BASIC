'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'assignment.bm - Parse rules for variable assignment

'Expects: lvalue token
'Results: token after rvalue
function ps_assignment
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start assignment"
    $end if
    root = ast_add_node(AST_ASSIGN)

    lval = ps_lvalue_mutable
    ast_attach root, lval
    ps_consume TOK_EQUALS

    expr = ps_expr
    lvalue_type = type_of_lvalue(lval)
    rvalue_type = type_of_expr(expr)
    if not type_can_cast(rvalue_type, lvalue_type) then ps_error "Type of variable in assignment does not match value being assigned"
    expr = ast_add_cast(expr, lvalue_type)
    ast_attach root, expr

    ps_assignment = root
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed assignment"
    $end if
end function

