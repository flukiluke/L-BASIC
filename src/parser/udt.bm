'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'udt.bm - Parser for user-defined types

'Expects: TOK_TYPE
'Results: token after END TYPE
function ps_udt
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start UDT definition"
    $end if
    dim udt_sym as symtab_entry_t
    udt_sym.typ = SYM_TYPE
    udt_sym.v1 = 0
    udt_sym.v2 = SYM_TYPE_UDT

    ps_consume TOK_TYPE
    if tok_token <> TOK_UNKNOWN then ps_error "UDT name already in use"
    udt_sym.identifier = ucase$(tok_content$)
    tok_advance
    ps_consume TOK_NEWLINE
    ps_line_prelude

    do
        elem = ps_udt_element(udt_sym)
        udt_sym.v1 = udt_sym.v1 + type_fixed_size(symtab(elem).v1)
        'If we ever need to keep a list of all the elements in a UDT,
        'this is where we'd build it.
    loop until ps_consumed(TOK_END)

    symtab_add_entry udt_sym
    ps_udt = 0 'Never generate any ast nodes
    ps_consume TOK_TYPE
    $if DEBUG_PARSE_TRACE then
    debuginfo "End UDT definition"
    $end if
end function

'Expects: Element identifier
'Results: token after newline
function ps_udt_element(udt_sym as symtab_entry_t)
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start UDT element"
    $end if
    dim elem_sym as symtab_entry_t
    elem_sym.typ = SYM_UDT_ELEMENT
    
    'Because UDT element names appear in such a restricted environment,
    'they can live in their own namespace without conflicting with
    'existing symbols. We just require that the name be an alphanumeric
    'identifier.
    elem_name$ = ucase$(tok_content$)
    select case left$(elem_name$, 1)
    case "A" to "Z", "_"
    case else
        print tok_content$
        ps_error "Invalid UDT element name"
    end select
    for i = 2 to len(elem_name$)
        select case mid$(elem_name$, i, 1)
        case "A" to "Z", "0" to "9", "_"
        case else
            ps_error "Invalid UDT element name"
        end select
    next i
    elem_sym.identifier = udt_sym.identifier + "." + elem_name$
    tok_advance

    'Set type from sigil, AS clause or default type
    elem_sym.v1 = ps_opt_sigil
    if elem_sym.v1 = 0 then elem_sym.v1 = ps_default_type

    'Get offset of element in UDT
    elem_sym.v2 = udt_sym.v1
    symtab_add_entry elem_sym
    
    ps_consume TOK_NEWLINE
    ps_line_prelude
    ps_udt_element = symtab_last_entry
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed UDT element"
    $end if
end function

'Expects: Element
'Results: token after element
'Takes udt variable as argument
function ps_udt_element_access(lvalue)
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start udt element access"
    $end if
    head_typ_name$ = symtab(type_of_lvalue(lvalue)).identifier
    elem = symtab_get_id(head_typ_name$ + "." + ucase$(tok_content$))
    if elem = 0 or symtab(elem).typ <> SYM_UDT_ELEMENT then ps_error "Bad UDT element access"
    tok_advance
    node = ast_add_node(AST_UDT_ACCESS)
    ast_attach node, lvalue
    ast_nodes(node).ref = elem
    ps_udt_element_access = node
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed udt element access"
    $end if
end function
