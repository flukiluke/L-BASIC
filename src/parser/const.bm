'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'const.bm - Parse rules for CONST

'Expects: TOK_CONST
'Results: token after rvalue
function ps_const
    $if DEBUG_PARSE_TRACE then
    debuginfo "Start const"
    $end if
    ps_consume TOK_CONST
    'We use a block so we can deal with multiple consts
    root = ast_add_node(AST_BLOCK)

    do
        if tok_token <> TOK_UNKNOWN then ps_error "CONST already defined"
        'Change default type because we want to infer it from the rvalue
        prev_default = ps_default_type
        ps_default_type = TYPE_NONE
        lval = ps_simple_variable
        var = ast_nodes(lval).ref

        ps_default_type = prev_default
        symtab(var).v3 = symtab(var).v3 or SYM_VARIABLE_CONST 'Mark constant
        node = ast_add_node(AST_ASSIGN)
        ast_attach root, node
        ast_attach node, lval
        ps_consume TOK_EQUALS

        expr = ps_expr
        if type_of_lvalue(lval) = TYPE_NONE then
            symtab(var).v1 = type_of_expr(expr)
            ast_attach node, expr
        elseif type_can_cast(symtab(var).v1, type_of_expr(expr)) then
            cast = ast_add_node(AST_CAST)
            ast_nodes(cast).ref = type_of_lvalue(lval)
            ast_attach cast, expr
            ast_attach node, cast
        else
            ps_error "Type mismatch"
        end if
    loop while ps_consumed(TOK_COMMA) 

    ps_const = root
    $if DEBUG_PARSE_TRACE then
    debuginfo "Completed const"
    $end if
end function
