'Copyright 2020 Luke Ceddia
'SPDX-License-Identifier: Apache-2.0
'function.bm - Parse rules for function calls

'Expects: function token
'Results: token after last argument
function ps_funccall
    token = tok_token
    tok_advance
    ps_funccall = ps_funccall_p(token)
end function

'Expects: token after function, function token as arg
'Results: token after last argument
function ps_funccall_p(func)
    debuginfo "Start function call"
    root = ast_add_node(AST_CALL)
    ast_nodes(root).ref = func
    sigil = ps_opt_sigil
    if tok_token = TOK_OPAREN then
        ps_consume TOK_OPAREN
        ps_funcargs root, type_sigt_create$(TYPE_ANY)
        ps_consume TOK_CPAREN
    else
       'function has no arguments
        matching_sig = type_find_sig_match(func, type_sigt_create$(TYPE_ANY))
        if matching_sig = 0 then ps_error "Cannot find matching type signature"
        ast_nodes(root).ref2 = matching_sig
        'Add any optional arguments at end
        for i = 1 to type_sig_numargs(matching_sig)
            ast_attach root, ast_add_node(AST_NONE)
        next i
    end if
    if sigil > 0 and sigil <> type_of_call(root) then ps_error "Function must have correct type suffix if present"
    ps_funccall_p = root
    debuginfo "Completed function call"
 end function

'Expects: first token after function name
'Results: token after argument list
sub ps_funcargs(root, candidate$)
    debuginfo "Start funcargs"
    'Handle empty argument at start
    if tok_token = TOK_COMMA then ps_funcarg root, candidate$
    'Loop over arguments and build candidate$ as a list of argument types
    do
        if ps_is_terminator(tok_token) or tok_token = TOK_CPAREN then exit do
        select case tok_token
        case TOK_COMMA
            ps_consume TOK_COMMA
            ps_funcarg root, candidate$
        case else
            ps_funcarg root, candidate$
        end select
    loop
    debuginfo "Parsed candidate: " + type_human_sig$(candidate$)
    'candidate$ may match multiple sigs; decide which fits "best". See type.bm for
    'details on what "best" means. This may still fail because the candidate has only
    'been checked as a prefix, never for an exect match on the number of arguments.
    best_sig = type_find_sig_match(ast_nodes(root).ref, candidate$)
    if best_sig = 0 then ps_error "Cannot find matching type signature"
    ast_nodes(root).ref2 = best_sig

    'Modify argument nodes to add in casts where needed
    for i = 1 to ast_num_children(root)
        expr = ast_get_child(root, i)
        expr_type = type_of_expr(expr)
        arg_type = type_sig_argtype(best_sig, i)
        if expr_type <> arg_type and expr_type <> TYPE_NONE then
            ast_replace_child root, i, ast_add_cast(expr, arg_type)
        end if
    next i
    'Add any optional arguments at end
    for i = ast_num_children(root) + 1 to type_sig_numargs(best_sig)
        ast_attach root, ast_add_node(AST_NONE)
    next i
    debuginfo "Completed funcargs"
end sub

sub ps_funcarg(root, candidate$)
    debuginfo "Start funcarg"
    func = ast_nodes(root).ref
    expects$ = type_sig_prefix_nexts$(func, candidate$)
    if ps_consumed(TOK_DOUBLE_SFX) then flags = TYPE_FILEHANDLE
    if tok_token = TOK_COMMA then
        'empty argument
        expr = ast_add_node(AST_NONE)
        flags = flags or TYPE_BYREF
    else
        expr = ps_expr
        if type_is_lvalue(expr) then flags = flags or TYPE_BYREF
    end if
    for i = 1 to type_sigt_numargs(expects$)
        expected_type = type_sigt_argtype(expects$, i)
        expected_flags = type_sigt_argflags(expects$, i)
        'it's unclear if "or expected_type = 0 and expected_flags = 0" is needed in
        'the below condition.
        if type_sig_compatible_arg(expected_type, expected_flags, type_of_expr(expr), flags, 0) then
            ok = TRUE
        end if
    next i
    if not ok then ps_error "Bad argument type"
    ast_attach root, expr
    candidate$ = type_sigt_add_arg(candidate$, type_of_expr(expr), flags)
    debuginfo "Completed funcarg"
end sub
